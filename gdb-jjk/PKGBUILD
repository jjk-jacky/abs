# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=gdb
pkgname=$_pkgname-jjk
pkgver=8.1.1
pkgrel=1
pkgdesc="The GNU Debugger"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/gdb/"
license=('GPL3')
depends=('ncurses' 'expat' 'python' 'xz' 'mpfr')
makedepends=('texinfo')
provides=($_pkgname)
conflicts=($_pkgname)
backup=('etc/gdb/gdbinit')
source=(https://ftp.gnu.org/gnu/gdb/${_pkgname}-${pkgver}.tar.xz{,.sig})
validpgpkeys=('F40ADB902B24264AA42E50BF92EDB04BFF325CF3')
md5sums=('372fa63a53adeee1bfbfd581d8da9e34'
         'SKIP')
validpgpkeys=('F40ADB902B24264AA42E50BF92EDB04BFF325CF3') # Joel Brobecker

prepare() {
  cd "${_pkgname}-${pkgver}"

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd "${_pkgname}-${pkgver}"

  ./configure --prefix=/usr --disable-nls \
    --with-system-readline \
    --with-python=/usr/bin/python \
    --with-system-gdbinit=/etc/gdb/gdbinit
  make
}

package() {
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install

  # install "custom" system gdbinit
  install -dm755 "$pkgdir"/etc/gdb
  touch "$pkgdir"/etc/gdb/gdbinit

  # resolve conflicts with binutils
  rm "$pkgdir"/usr/include/{ansidecl,bfd,bfdlink,dis-asm,plugin-api,symcat}.h
  rm "$pkgdir"/usr/share/info/bfd.info
  rm "$pkgdir"/usr/lib/lib{bfd,opcodes}.a
}
