# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_pkgname=claws-mail
pkgname=$_pkgname-jjk
pkgver=3.17.1
pkgrel=1
pkgdesc="A GTK+ based e-mail client."
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org"
options=('debug' '!strip')
depends=('gtk2' 'gnutls' 'startup-notification' 'gpgme' 'libetpan'
         'libsm' 'dbus-glib' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('valgrind'
             # dependencies for plugins
             'libnotify' 'pygtk') 
optdepends=('python2:           needed for some tools and python plugin'
            'perl:              needed for some tools and perl plugin'
            'bogofilter:        adds support for spamfiltering'
            'libnotify:         for notification plugin'
            'dbus:              for notification plugin')
replaces=('sylpheed-claws' 'claws-mail-extra-plugins')
conflicts=("$_pkgname" 'claws-mail-extra-plugins')
provides=("$_pkgname" 'claws')
source=(http://www.claws-mail.org/download.php?file=releases/claws-mail-$pkgver.tar.xz{,.asc}
        0001-Fix-diff-format-detection.patch
        0002-Add-coloring-of-diff-patches-inside-emails.patch
        0003-Always-sort-by-date-ASC-within-a-thread-when-by-thre.patch
        0004-Allow-external-use-of-strings-about-Extended-Search.patch
        0005-Add-vfolder-plugin.patch
        0006-quicksearch-expand-extended-symbols-on-Edit.patch
        0007-Add-a-signature.asc-name-for-PGP-MIME-signatures.patch
        0008-Speed-up-searches-in-References-via-x.patch
        0009-prefs-add-option-Ignore-sort-direction-Make-sort-ord.patch
        0010-Add-Delivery-Status-Notification-DSN-support.patch
        0011-action-Keep-selection-set-afterwards.patch
        0012-Preserve-flags-tags-when-re-editing-a-message.patch)
sha256sums=('757b5f4163fb44f772b1453d1cc0b83387bb6b67ad55cc0694bdd4eff9ea76f2'
            'SKIP'
            'e3a29401054e81c9b76e2a0607a364571a945fc2a88e1a2d9c7ef523a73656aa'
            '8d229fe392c6699c7bff769872ee07ba1da3bd1371e2cb7784236fc317ab90e9'
            'a27ec811b997b6cd86e6d87a5f7ca24df7ae4e06217b6eaca624e931a933f941'
            '75a97a889f95ed4b7627055dc7c8d336e1c3e36ab2b367198a192aa2560c2f83'
            'fe9017da938e078b9b9720b3ad5e279f49827d7f5e0250622c9025851928d108'
            '353ddb7bc8ffdae927afecccbbe70be140db4a414e3ccfd96245c925df75b5ad'
            'dc7e0762cd20e9350a2c90b67bf9f0f6893008f948f3134e9abf90224457a838'
            '7e01afc7aa5a0abdf6fc93ae7324476f1824a9dbe1e82991a7e328d8fec527e7'
            '7aa45eaa31f746eb2ffdc3223cdbc8eb6e0645b3e02be17b61302fb6e1273aba'
            'd517c3c7b061ceab3494a12d53e5678ab25fff179de9834bacecc4cf1860faeb'
            'bae66fcdaf4d3b0cadf6193a47aa20b35c11db961980d7b8d15503cdc44885d7'
            '7eb3a8367a9d3363db489ac1805992631b4f22a1487848315a3d778511e9357d')
validpgpkeys=('8B3B297A03468356692F8D592CD716D654D6BBD4') # Paul <paul@claws-mail.org>

prepare() {
  cd "${_pkgname}-${pkgver}"
  for f in "${source[@]}"; do
      [[ "$f" = *".patch" ]] && patch -p1 -i ../"$f"
  done
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${_pkgname}-${pkgver}"

  # fixes for python2
  export PYTHON="/usr/bin/python2"
  sed -i 's@^#!.*python.*@#!/usr/bin/python2@' tools/*.py
  sed -i 's:python -c:python2 -c:g' configure

  ./configure --prefix=/usr --disable-static \
    --disable-enchant \
    --disable-compface \
    --enable-gnutls \
    --enable-ldap \
    --enable-crash-dialog \
    --enable-att_remover-plugin \
    --enable-attachwarner-plugin \
    --enable-bogofilter-plugin \
    --enable-notification-plugin \
    --enable-pgpcore-plugin \
    --enable-pgpmime-plugin \
    --enable-python-plugin \
    --enable-vfolder-plugin \
    --disable-acpi_notifier-plugin \
    --disable-address_keeper-plugin \
    --disable-archive-plugin \
    --disable-bsfilter-plugin \
    --disable-clamd-plugin \
    --disable-fancy-plugin \
    --disable-fetchinfo-plugin \
    --disable-gdata-plugin \
    --disable-libravatar-plugin \
    --disable-mailmbox-plugin \
    --disable-managesieve-plugin \
    --disable-pdf_viewer-plugin \
    --disable-rssyl-plugin \
    --disable-spamassassin-plugin \
    --disable-spam_report-plugin \
    --disable-tnef_parse-plugin \
    --disable-vcalendar-plugin
  make
}

package() {
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # install extra tools
  cd tools
  install -m755 -d "$pkgdir"/usr/lib/claws-mail/tools
  for file in *.pl *.py *.sh multiwebsearch.conf tb2claws-mail update-po \
      uudec uuooffice README; do
      cp -av "$file" "$pkgdir"/usr/lib/claws-mail/tools
  done
}
