# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_pkgname=claws-mail
pkgname=$_pkgname-jjk
pkgver=3.17.1
pkgrel=2
pkgdesc="A GTK+ based e-mail client."
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org"
options=('debug' '!strip')
depends=('gtk2' 'gnutls' 'startup-notification' 'gpgme' 'libetpan'
         'libsm' 'dbus-glib' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('valgrind'
             # dependencies for plugins
             'libnotify' 'pygtk') 
optdepends=('python2:           needed for some tools and python plugin'
            'perl:              needed for some tools and perl plugin'
            'bogofilter:        adds support for spamfiltering'
            'libnotify:         for notification plugin'
            'dbus:              for notification plugin')
replaces=('sylpheed-claws' 'claws-mail-extra-plugins')
conflicts=("$_pkgname" 'claws-mail-extra-plugins')
provides=("$_pkgname" 'claws')
source=(http://www.claws-mail.org/download.php?file=releases/claws-mail-$pkgver.tar.xz{,.asc}
        0001-Fix-diff-format-detection.patch
        0002-Add-coloring-of-diff-patches-inside-emails.patch
        0003-Always-sort-by-date-ASC-within-a-thread-when-by-thre.patch
        0004-Allow-external-use-of-strings-about-Extended-Search.patch
        0005-Add-vfolder-plugin.patch
        0006-quicksearch-expand-extended-symbols-on-Edit.patch
        0007-Add-a-signature.asc-name-for-PGP-MIME-signatures.patch
        0008-Speed-up-searches-in-References-via-x.patch
        0009-prefs-add-option-Ignore-sort-direction-Make-sort-ord.patch
        0010-Add-Delivery-Status-Notification-DSN-support.patch
        0011-action-Keep-selection-set-afterwards.patch
        0012-Preserve-flags-tags-when-re-editing-a-message.patch)
sha256sums=('757b5f4163fb44f772b1453d1cc0b83387bb6b67ad55cc0694bdd4eff9ea76f2'
            'SKIP'
            '9ba9074f1315469fa556f0bdba859ebcb005906341d83f7307103bd22f8594fc'
            '908350dcdfb887ac736c6055ca7af535431457d9286d39036f9829c9b7832238'
            '7e0c3de39535d45d3c05e906a3c529dfb2d7a5b002ec4453ddd1359d68ce6327'
            '7055b9fb300f6d8e320f92e0251f8cde264b08624708b9984c9d191180301751'
            '4db06c335494c03aecbd0cea40dc7b90d0def0acfe6f51a9a0b0ab2cbf2dfd45'
            'f53c2359103f21104ad73bf94e1705709a19089a4f4af2758bf9e8a2e72ee45b'
            '7b822d0ad32e53030862f720af57556182524003cd306643a6cb1668e9b862ff'
            '39edb1de2bcdb5db00a07be4a8544371f1771d0b416d3e800ab91aa388cba616'
            '6de070ac2d04787cd6397771b4a9fdf1d9fad4e597510dbf0fc9b9dee3c7b28c'
            '27f666e03505701bdd8f83aa01d35e3c9685b7d2a6592abbe074ef3a7f0e0edc'
            '0defc0fa08014cfdc6e2e269072436eb7ae2316d2a58da4d6e4513100097e7ac'
            '650addc4259565035d1397b317ec82b9ce11f39c543d5362eca7d0704cf6a8eb')
validpgpkeys=('8B3B297A03468356692F8D592CD716D654D6BBD4') # Paul <paul@claws-mail.org>

prepare() {
  cd "${_pkgname}-${pkgver}"
  for f in "${source[@]}"; do
      [[ "$f" = *".patch" ]] && patch -p1 -i ../"$f"
  done
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${_pkgname}-${pkgver}"

  # fixes for python2
  export PYTHON="/usr/bin/python2"
  sed -i 's@^#!.*python.*@#!/usr/bin/python2@' tools/*.py
  sed -i 's:python -c:python2 -c:g' configure

  ./configure --prefix=/usr --disable-static \
    --disable-enchant \
    --disable-compface \
    --enable-gnutls \
    --enable-ldap \
    --enable-crash-dialog \
    --enable-att_remover-plugin \
    --enable-attachwarner-plugin \
    --enable-bogofilter-plugin \
    --enable-notification-plugin \
    --enable-pgpcore-plugin \
    --enable-pgpmime-plugin \
    --enable-python-plugin \
    --enable-vfolder-plugin \
    --disable-acpi_notifier-plugin \
    --disable-address_keeper-plugin \
    --disable-archive-plugin \
    --disable-bsfilter-plugin \
    --disable-clamd-plugin \
    --disable-fancy-plugin \
    --disable-fetchinfo-plugin \
    --disable-gdata-plugin \
    --disable-libravatar-plugin \
    --disable-mailmbox-plugin \
    --disable-managesieve-plugin \
    --disable-pdf_viewer-plugin \
    --disable-rssyl-plugin \
    --disable-spamassassin-plugin \
    --disable-spam_report-plugin \
    --disable-tnef_parse-plugin \
    --disable-vcalendar-plugin
  make
}

package() {
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # install extra tools
  cd tools
  install -m755 -d "$pkgdir"/usr/lib/claws-mail/tools
  for file in *.pl *.py *.sh multiwebsearch.conf tb2claws-mail update-po \
      uudec uuooffice README; do
      cp -av "$file" "$pkgdir"/usr/lib/claws-mail/tools
  done
}
