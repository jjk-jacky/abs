# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Paul Mattal <paul@archlinux.org>

_pkgname=ffmpeg
pkgname=$_pkgname-jjk
pkgver=4.0
pkgrel=1
pkgdesc="Complete and free Internet live audio and video broadcasting solution for Linux/Unix"
arch=('i686' 'x86_64')
url="https://ffmpeg.org/"
license=('GPL')
depends=(
      'alsa-lib' 'bzip2' 'fontconfig' 'lame'
      'libpulse' 'libvorbis' 'libvpx'
      'sdl' 'libxml2'
      'v4l-utils' 'libx264' 'xvidcore' 'zlib'
      'libx264.so'
      'libva-drm.so' 'libva.so' 'libva-x11.so'
      )
makedepends=('yasm')
provides=($_pkgname 'libavcodec.so' 'libavdevice.so' 'libavfilter.so'
          'libavformat.so' 'libavresample.so' 'libavutil.so' 'libpostproc.so'
          'libswresample.so' 'libswscale.so')
conflicts=($_pkgname)
source=(https://ffmpeg.org/releases/$_pkgname-$pkgver.tar.xz{,.asc})
validpgpkeys=('FCF986EA15E6E293A5644F10B4322F04D67658D8') # ffmpeg-devel
sha256sums=('ed945daf40b124e77a685893cc025d086f638bc703183460aff49508edb3a43f'
            'SKIP')

build() {
  cd $_pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-debug \
    --disable-static \
    --disable-stripping \
    --enable-avresample \
    --enable-fontconfig \
    --enable-gpl \
    --enable-libfreetype \
    --enable-libmp3lame \
    --enable-libpulse \
    --enable-libv4l2 \
    --enable-libvorbis \
    --enable-libvpx \
    --enable-libx264 \
    --enable-libxvid \
    --enable-libxml2 \
    --enable-shared \
    --enable-version3 \
    --disable-protocol=concat

  make
  make tools/qt-faststart
  make doc/ff{mpeg,play}.1
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install install-man
  install -D -m755 tools/qt-faststart "$pkgdir/usr/bin/qt-faststart"
}

# vim:set ts=2 sw=2 et:
