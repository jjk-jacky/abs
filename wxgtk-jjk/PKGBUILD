# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

_pkgname=wxgtk
pkgname=$_pkgname-jjk
pkgver=3.0.4
pkgrel=2
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="https://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'libsm' 'libxxf86vm')
makedepends=('gconf' 'glu')
conflicts=($_pkgname)
provides=($_pkgname)
options=('!emptydirs')
source=(https://github.com/wxWidgets/wxWidgets/releases/download/v$pkgver/wxWidgets-$pkgver.tar.bz2)
sha1sums=('246561a73ec5b9a5a7aaaaed46b64515dc9039ab')

build() {
  cd wxWidgets-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --disable-precomp-headers
  make
  make -C locale allmo
}

package() {
  cd wxWidgets-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 docs/licence.txt "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}
