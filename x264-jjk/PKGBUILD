# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=('x264-jjk' 'libx264-jjk')
pkgver=155.r0a84d986
pkgrel=1
arch=('i686' 'x86_64')
url='https://www.videolan.org/developers/x264.html'
license=('GPL')
makedepends=('nasm' 'git' 'ffmpeg' 'l-smash')
_commit='0a84d986e7020f8344f00752e3600b9769cc1e85'
source=("git://git.videolan.org/x264.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd x264

  local _ver=$(grep '#define X264_BUILD' x264.h | cut -d' ' -f3)
  local _rev=$(git rev-parse --short HEAD)

  echo ${_ver}.r${_rev}
}

build() {
  cd x264

  ./configure \
    --prefix=/usr \
    --enable-shared \
    --enable-pic
  make
}

package_x264-jjk() {
  pkgdesc='CLI tools for encoding H264/AVC video streams'
  depends+=('libavcodec.so' 'libavformat.so' 'libavutil.so' 'liblsmash.so'
            'libswscale.so')
  provides=('x264')
  conflicts=('x264')

  make -C x264 DESTDIR="$pkgdir" install-cli
}

package_libx264-jjk() {
  pkgdesc='Library for encoding H264/AVC video streams'
  provides=('libx264' 'libx264.so' 'x264-dev')
  conflicts=('libx264' 'x264-dev')
  replaces=('x264-dev')

  install -dm 755 "$pkgdir"/usr/lib
  make -C x264 DESTDIR="$pkgdir" install-lib-shared
}
