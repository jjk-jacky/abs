# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgname=glib2
pkgname=$_pkgname-jjk
pkgver=2.56.2
pkgrel=1
pkgdesc="Common C routines used by GTK+ and other libs"
url="http://www.gtk.org/"
arch=(i686 x86_64)
license=('LGPL')
depends=('pcre' 'libffi' 'libutil-linux')
makedepends=('util-linux' 'pkg-config' 'python' 'libxslt' 'docbook-xml')
optdepends=('python2: for gdbus-codegen')
provides=($_pkgname)
conflicts=($_pkgname)
options=('!emptydirs' '!strip' 'debug')
source=(https://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-$pkgver.tar.xz
        glib-$pkgver.tar.xz.sha256::https://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-$pkgver.sha256sum
        glib-compile-schemas.hook
        gio-querymodules.hook
        noisy-glib-compile-schemas.diff
        0001-GRWLock-Tweak-doc-to-make-things-a-bit-clearer.patch
        0002-GRWLock-Tweak-re-blocking-on-g_rw_lock_reader_lock.patch)
sha256sums=('d64abd16813501c956c4e123ae79f47f1b58de573df9fdd3b0795f1e2c1aa789'
            '35c7e3ff01549c907e971c6a8922dfcc9cb9c1a37ab6c1b50b588c9bfe5a1a07'
            'e1123a5d85d2445faac33f6dae1085fdd620d83279a4e130a83fe38db52b62b3'
            '5ba204a2686304b1454d401a39a9d27d09dd25e4529664e3fd565be3d439f8b6'
            '81a4df0b638730cffb7fa263c04841f7ca6b9c9578ee5045db6f30ff0c3fc531'
            '6ead7518c2e543ed459e6e4239939f43f14304d152c7591879dc82bee0d61a08'
            '974f512987e44103745890ca7327d855a5b81d0b7729cb0082adf30d8ea016fd')

prepare() {
  cd "glib-$pkgver"

  patch -p1 -i ../noisy-glib-compile-schemas.diff
  patch -p1 -i ../0001-GRWLock-Tweak-doc-to-make-things-a-bit-clearer.patch
  patch -p1 -i ../0002-GRWLock-Tweak-re-blocking-on-g_rw_lock_reader_lock.patch
}

build() {
  cd "glib-$pkgver"

  ./configure --prefix=/usr --libdir=/usr/lib \
      --sysconfdir=/etc \
      --with-pcre=system \
      --disable-fam
  make
}

package() {
  cd glib-$pkgver

  make completiondir=/usr/share/bash-completion/completions DESTDIR="$pkgdir" install

  for _i in "$pkgdir/usr/share/bash-completion/completions/"*; do
      chmod -x "$_i"
  done
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir"/usr/bin/gdbus-codegen

  # install hooks
  install -dm755 "$pkgdir"/usr/share/libalpm/hooks/
  install -m644 "$srcdir"/{glib-compile-schemas,gio-querymodules}.hook "$pkgdir"/usr/share/libalpm/hooks/
}
