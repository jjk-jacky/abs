# $Id: PKGBUILD 173315 2012-12-18 20:30:41Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

_pkgbase=pulseaudio
pkgbase=$_pkgbase-jjk
pkgname=(pulseaudio-jjk libpulse-jjk)
pkgdesc="A featureful, general-purpose sound server"
pkgver=8.0
pkgrel=1
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/PulseAudio"
license=(GPL LGPL)
makedepends=(libasyncns libcap attr libxtst libsm libsndfile libtool rtkit
             gdbm dbus avahi gconf intltool
             openssl orc json-c gtk3)
options=(!emptydirs)
source=(http://freedesktop.org/software/$_pkgbase/releases/$_pkgbase-$pkgver.tar.xz)
sha256sums=('690eefe28633466cfd1ab9d85ebfa9376f6b622deec6bfee5091ac9737cd1989')

build() {
  cd $_pkgbase-$pkgver

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --with-database=gdbm \
    --disable-systemd-daemon \
    --disable-systemd-login \
    --disable-systemd-journal \
    --disable-udev \
    --disable-tcpwrap \
    --disable-rpath \
    --disable-bluez4 \
    --disable-bluez5 \
    --disable-default-build-tests \
    --disable-jack \
    --disable-esound \
    --disable-oss-output \
    --disable-oss-wrapper \
    --without-fftw \
    --disable-lirc \
    --disable-webrtc-aec \
    --without-speex \
    DATADIRNAME=share

  # fight unused direct deps
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package_pulseaudio-jjk() {
  depends=("libpulse=$pkgver-$pkgrel" rtkit libltdl gdbm orc)
  optdepends=('pulseaudio-alsa: ALSA configuration (recommended)'
              'avahi: zeroconf support'
              'gconf: configuration through gconf (paprefs)'
              'openssl: RAOP support')
  backup=(etc/pulse/{daemon.conf,default.pa,system.pa})
  install=pulseaudio.install
  replaces=(pulseaudio)
  provides=(pulseaudio)
  conflicts=(pulseaudio)

  cd $_pkgbase-$pkgver
  make -j1 DESTDIR="$pkgdir" install

  # Speed up pulseaudio shutdown so that it exists immediately with
  # the last user session (module-systemd-logon keeps it alive)
  sed -e '/exit-idle-time/iexit-idle-time=0' \
      -i "$pkgdir/etc/pulse/daemon.conf"

  # Disable cork-request module, can result in e.g. media players unpausing
  # when there's a Skype call incoming
  sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' \
      -i "$pkgdir/usr/bin/start-pulseaudio-x11"

  rm "$pkgdir/etc/dbus-1/system.d/pulseaudio-system.conf"

### Split libpulse

  mkdir -p "$srcdir"/libpulse/{etc/pulse,usr/{bin,lib/pulseaudio,share/man/man{1,5}}}

  mv {"$pkgdir","$srcdir/libpulse"}/etc/pulse/client.conf

  mv "$pkgdir"/usr/bin/pa{cat,ctl,mon,play,rec,record} \
     "$srcdir/libpulse/usr/bin"

  mv "$pkgdir"/usr/lib/libpulse{,-simple,-mainloop-glib}.so* \
     "$srcdir/libpulse/usr/lib"

  mv "$pkgdir"/usr/lib/pulseaudio/libpulsecommon-*.so \
     "$srcdir/libpulse/usr/lib/pulseaudio"

  mv {"$pkgdir","$srcdir/libpulse"}/usr/lib/cmake
  mv {"$pkgdir","$srcdir/libpulse"}/usr/lib/pkgconfig

  mv {"$pkgdir","$srcdir/libpulse"}/usr/include

  rm "$pkgdir"/usr/share/man/man1/padsp.1
  mv "$pkgdir"/usr/share/man/man1/pa{cat,ctl,play}.1 \
     "$srcdir/libpulse/usr/share/man/man1"

  mv {"$pkgdir","$srcdir/libpulse"}/usr/share/man/man5/pulse-client.conf.5
  mv {"$pkgdir","$srcdir/libpulse"}/usr/share/vala
}

package_libpulse-jjk() {
  pkgdesc="$pkgdesc (client library)"
  depends=(dbus libasyncns libcap libxtst libsm libsndfile json-c)
  backup=(etc/pulse/client.conf)
  replaces=(libpulse)
  provides=("libpulse=$pkgver-$pkgrel")
  conflicts=(libpulse)

  mv "$srcdir"/libpulse/* "$pkgdir"
}
