# Contributor: Lukas Braun <koomi+aur at hackerspace-bamberg dot de>
# Contributor: David Arroyo <droyo@aqwari.us>
_pkgname=s6-dns
pkgname=$_pkgname-musl-git
pkgver=v2.0.1.0
pkgrel=1
pkgdesc="skarnet's DNS client suite [static-musl; GIT]"
arch=('i686' 'x86_64')
url="http://www.skarnet.org/software/s6-dns"
license=('custom:ISC')
makedepends=('skalibs-musl' 'execline-musl' 'musl')
options=('staticlibs')
source=("git://git.skarnet.org/s6-dns")
sha256sums=('SKIP')
provides=($_pkgname $_pkgname-musl)
conflicts=($_pkgname $_pkgname-musl)

pkgver() {
    cd $_pkgname
    git describe --abbrev=4 --dirty | tr - .
}

build() {
  cd $_pkgname

  export CPPFLAGS='-nostdinc -isystem /usr/lib/musl/include -isystem /usr/include'
  export CC="musl-gcc"
  ./configure --enable-static-libc --bindir=/usr/bin --sbindir=/usr/bin \
      --libexecdir=/usr/lib/s6-dns
  make
}

package() {
  cd $_pkgname

  make DESTDIR="$pkgdir/" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}
