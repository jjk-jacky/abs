# $Id: PKGBUILD 158788 2012-05-09 18:48:29Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=php
pkgname=('php-jjk'
         'php-fpm-jjk')
pkgver=7.2.5
pkgrel=1
arch=('i686' 'x86_64')
license=('PHP')
url='https://secure.php.net'
makedepends=('db' 'gmp' 'pcre'
             'libxml2' 'bzip2' 'curl')
source=("https://php.net/distributions/${pkgbase}-${pkgver}.tar.xz"{,.asc}
        'php.ini.patch' 'php-fpm.patch')
validpgpkeys=('1729F83938DA44E27BA0F4D3DBDB397470D12172'
              'B1B44D8F021E4E2D6021E995DC9FF8D3EE5AF27F')
sha256sums=('af70a33b3f7a51510467199b39af151333fbbe4cc21923bad9c7cf64268cddb2'
            'SKIP'
            'c41a5bec7a8f6c84c6f27bf037f6882fdca46a469651c2cd3be7286aa8de11dd'
            '9d2e72a0279711b9c1b256937d8decacc0c9793f5cfc20094e7875b2a59e16a4')

prepare() {
    cd "${srcdir}/${pkgbase}-${pkgver}"

    patch -p0 -i ../php.ini.patch
    patch -p0 -i ../php-fpm.patch
}

build() {
    phpconfig="--srcdir=../${pkgbase}-${pkgver} \
        --config-cache \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --sysconfdir=/etc/php \
        --localstatedir=/var \
        --with-layout=GNU \
        --with-config-file-path=/etc/php \
        --with-config-file-scan-dir=/etc/php/conf.d \
        --disable-rpath \
        --mandir=/usr/share/man \
        --without-pear \
        --enable-zend-signals \
        "

    phpextensions="--enable-bcmath=shared \
        --enable-calendar=shared \
        --enable-dba=shared \
        --enable-exif=shared \
        --enable-ftp=shared \
        --enable-mbstring \
        --enable-shmop=shared \
        --enable-soap=shared \
        --enable-sockets=shared \
        --enable-sysvmsg=shared \
        --enable-sysvsem=shared \
        --enable-sysvshm=shared \
        --enable-zip=shared \
        --with-bz2=shared \
        --with-curl=shared \
        --with-db4=/usr \
        --with-freetype-dir=/usr \
        --with-gd=shared \
        --with-gettext=shared \
        --with-gmp=shared \
        --with-iconv=shared \
        --with-mhash \
        --with-openssl \
        --with-pcre-regex=/usr \
        --with-xmlrpc=shared \
        --with-zlib \
        --enable-pcntl \
        "

    EXTENSION_DIR=/usr/lib/php/modules
    export EXTENSION_DIR

    mkdir "${srcdir}/build"
    cd "${srcdir}/build"
    ln -s ../${pkgbase}-${pkgver}/configure
    ./configure ${phpconfig} \
        --enable-cgi \
        --enable-fpm \
        --with-fpm-user=http \
        --with-fpm-group=http \
        ${phpextensions}
    make
}

package_php-jjk() {
	pkgdesc='An HTML-embedded scripting language'
	depends=('libxml2' 'bzip2' 'curl' 'pcre')
	provides=('php')
	conflicts=('php')
	backup=('etc/php/php.ini')

	cd "${srcdir}/build"
	make -j1 INSTALL_ROOT="${pkgdir}" install-{modules,cli,build,headers,programs,pharcmd}
	# install php.ini
	install -D -m644 "${srcdir}"/${pkgbase}-${pkgver}/php.ini-production "${pkgdir}"/etc/php/php.ini
	install -d -m755 "${pkgdir}"/etc/php/conf.d/

	# remove static modules
	rm -f "${pkgdir}"/usr/lib/php/modules/*.a
	# remove modules provided by sub packages
	rm -f "${pkgdir}"/usr/lib/php/modules/{enchant,gd,intl,ldap,mcrypt,mssql,odbc,pdo_odbc,pgsql,pdo_pgsql,pspell,snmp,sqlite3,pdo_sqlite,tidy,xsl}.so
	# remove empty directory
	rmdir "${pkgdir}"/usr/include/php/include
}

package_php-fpm-jjk() {
	pkgdesc='FastCGI Process Manager for PHP'
	depends=('php')
	conflicts=('php-fpm')
	backup=('etc/php/php-fpm.conf' 'etc/php/php-fpm.d/www.conf')
    options=('!emptydirs')

    cd "${srcdir}"/build
    make -j1 INSTALL_ROOT="${pkgdir}" install-fpm
}
