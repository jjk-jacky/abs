# $Id: PKGBUILD 169943 2012-10-30 22:46:13Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgname=harfbuzz
pkgname=$_pkgname-jjk
pkgver=1.9.0
pkgrel=1
pkgdesc="OpenType text shaping engine."
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=('MIT')
depends=('glib2' 'freetype2')
makedepends=('cairo' 'glib2' 'freetype2' 'python')
optdepends=('cairo: hb-view program')
checkdepends=(python-fonttools python-setuptools)
provides=('harfbuzz')
conflicts=('harfbuzz')
options=(!libtool)
source=(https://www.freedesktop.org/software/harfbuzz/release/${_pkgname}-${pkgver}.tar.bz2
        ${_pkgname}-${pkgver}.tar.bz2.sha256.signed-msg::https://www.freedesktop.org/software/harfbuzz/release/${_pkgname}-${pkgver}.tar.bz2.sha256.asc)
validpgpkeys=('2277650A4E8BDFE4B7F6BE419FEE04E5D3531115')
sha256sums=('SIGNED-MSG' 'SKIP')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  export LANG=en_US.utf8
  ./configure --prefix=/usr --with-glib --with-gobject --with-cairo \
      --with-freetype
  make
}

_check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/harfbuzz/COPYING"
}
