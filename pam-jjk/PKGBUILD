# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

_pkgname=pam
pkgname=$_pkgname-jjk
pkgver=1.3.1
pkgrel=1
pkgdesc="PAM (Pluggable Authentication Modules) library"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.kernel.org/pub/linux/libs/pam/"
depends=('glibc' 'db' 'cracklib' 'libtirpc' 'pambase')
makedepends=('flex' 'w3m' 'docbook-xml>=4.4' 'docbook-xsl')
conflicts=($_pkgname)
provides=($_pkgname)
backup=(etc/security/{access.conf,group.conf,limits.conf,namespace.conf,namespace.init,pam_env.conf,time.conf} etc/default/passwd etc/environment)
source=(https://github.com/linux-pam/linux-pam/releases/download/v$pkgver/Linux-PAM-$pkgver.tar.xz{,.asc})
validpgpkeys=('AF7402BC38CC10E6885C1FCA421784ABD41A6DF2' # Andrew G. Morgan <morgan@kernel.org>
              '8C6BFD92EE0F42EDF91A6A736D1A7F052E5924BB' # Thorsten Kukuk (privat) <kukuk@thkukuk.de>
             )
options=('!emptydirs')
md5sums=('558ff53b0fc0563ca97f79e911822165'
         'SKIP')

build() {
  cd "$srcdir/Linux-PAM-$pkgver"
  ./configure --libdir=/usr/lib --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/Linux-PAM-$pkgver"
  make DESTDIR="$pkgdir" SCONFIGDIR=/etc/security install

  # set unix_chkpwd uid
  chmod +s "$pkgdir/usr/bin/unix_chkpwd"
}
