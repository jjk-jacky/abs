# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=mesa-jjk
pkgname=('ati-dri-jjk' 'intel-dri-jjk' 'mesa-jjk' 'mesa-libgl-jjk')
pkgver=17.3.1
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('python2' 'libxml2' 'libx11' 'glproto' 'libdrm' 'dri2proto'
             'dri3proto' 'presentproto' 'libxxf86vm' 'libxdamage' 'libelf'
             'libxshmfence' 'python2-mako' 'libpthread-stubs')
url="https://www.mesa3d.org/"
license=('custom')
source=(https://mesa.freedesktop.org/archive/mesa-$pkgver.tar.xz{,.sig}
        LICENSE)
validpgpkeys=('CEF8DDC8019CA66D4E3094F3600233BA9E54DC61'
              '8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D'
              '946D09B5E4C9845E63075FF1D961C596A7203456'
              'E3E8F480C52ADD73B278EE78E1ECBE07D7D70895')
sha256sums=('9ae607e0998a586fb2c866cfc8e45e6f52d1c56cb1b41288253ea83eada824c1'
            'SKIP'
            '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2')

build() {
    cd mesa-$pkgver

    #autoreconf -vfi # our automake is far too new for their build system :)

    ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-gallium-drivers=r600,swrast \
    --with-dri-drivers=radeon,swrast,i965 \
    --with-platforms=x11,drm \
    --enable-egl \
    --enable-gbm \
    --enable-shared-glapi \
    --enable-glx \
    --enable-glx-tls \
    --enable-dri \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float

    make
    # fake installation
    mkdir $srcdir/fakeinstall
    make DESTDIR=${srcdir}/fakeinstall install
}

package_ati-dri-jjk() {
  pkgdesc="Mesa drivers for AMD/ATI Radeon"
  depends=("mesa-libgl-jjk=${pkgver}")
  conflicts=('xf86-video-ati<6.9.0-6' 'ati-dri' 'mesa-dri')
  provides=('ati-dri' 'mesa-dri')
  replaces=('ati-dri' 'mesa-dri')

  install -m755 -d ${pkgdir}/usr/lib/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/{r600,radeon}_dri.so ${pkgdir}/usr/lib/dri/

  install -m755 -d "${pkgdir}/usr/share/licenses/ati-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/ati-dri/"
}

package_intel-dri-jjk() {
  pkgdesc="Mesa drivers for Intel"
  depends=("mesa-libgl-jjk=${pkgver}")
  conflicts=('intel-dri' 'mesa-dri')
  provides=('intel-dri' 'mesa-dri')
  replaces=('intel-dri' 'mesa-dri')

  install -m755 -d ${pkgdir}/usr/lib/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/i965_dri.so ${pkgdir}/usr/lib/dri/

  install -m755 -d "${pkgdir}/usr/share/licenses/intel-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/intel-dri/"
}

package_mesa-jjk() {
  pkgdesc="an open-source implementation of the OpenGL specification"
  depends=('libdrm' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'libelf'
           'libgl')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=("mesa=${pkgver}")
  conflicts=('mesa')

  mv -v ${srcdir}/fakeinstall/* ${pkgdir}
  install -m755 -d ${pkgdir}/usr/lib/mesa
  # move libgl/EGL/glesv*.so to not conflict with blobs - may break .pc files ?
  mv -v ${pkgdir}/usr/lib/libGL.so* ${pkgdir}/usr/lib/mesa/
  mv -v ${pkgdir}/usr/lib/libEGL.so* ${pkgdir}/usr/lib/mesa/
  mv -v ${pkgdir}/usr/lib/libGLES*.so* ${pkgdir}/usr/lib/mesa/

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa/"
}

package_mesa-libgl-jjk() {
  pkgdesc="Mesa 3-D graphics library"
  depends=('mesa')
  provides=('libgl' 'libegl' 'libgles')
  conflicts=('libgl' 'libegl' 'libgles')

  # See FS#26284
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  ln -s /usr/lib/mesa/libGL.so.1.2.0 ${pkgdir}/usr/lib/libGL.so.1.2.0
  ln -s libGL.so.1.2.0 ${pkgdir}/usr/lib/libGL.so.1
  ln -s libGL.so.1.2.0 ${pkgdir}/usr/lib/libGL.so

  ln -s /usr/lib/mesa/libEGL.so.1.0.0 ${pkgdir}/usr/lib/libEGL.so.1.0.0
  ln -s libEGL.so.1.0.0 ${pkgdir}/usr/lib/libEGL.so.1
  ln -s libEGL.so.1.0.0 ${pkgdir}/usr/lib/libEGL.so

  ln -s /usr/lib/mesa/libGLESv1_CM.so.1.1.0 ${pkgdir}/usr/lib/libGLESv1_CM.so.1.1.0
  ln -s libGLESv1_CM.so.1.1.0 ${pkgdir}/usr/lib/libGLESv1_CM.so.1
  ln -s libGLESv1_CM.so.1.1.0 ${pkgdir}/usr/lib/libGLESv1_CM.so

  ln -s /usr/lib/mesa/libGLESv2.so.2.0.0 ${pkgdir}/usr/lib/libGLESv2.so.2.0.0
  ln -s libGLESv2.so.2.0.0 ${pkgdir}/usr/lib/libGLESv2.so.2
  ln -s libGLESv2.so.2.0.0 ${pkgdir}/usr/lib/libGLESv2.so

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa-libgl"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa-libgl/"
}
