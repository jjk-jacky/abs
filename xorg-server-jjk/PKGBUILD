# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=xorg-server
pkgname=(xorg-server-jjk xorg-server-xephyr-jjk)
pkgver=1.20.0
pkgrel=1 # build first with 0.1 and then rebuild it after xf86-input-evdev rebuild
arch=('i686' 'x86_64')
license=('custom')
url="https://xorg.freedesktop.org"
makedepends=('pixman' 'libx11' 'mesa' 'libgl' 'xf86driproto' 'xcmiscproto' 'xtrans' 'bigreqsproto' 'randrproto' 
             'inputproto' 'fontsproto' 'videoproto' 'presentproto' 'compositeproto' 'recordproto' 'scrnsaverproto'
             'resourceproto' 'xineramaproto' 'libxkbfile' 'libxfont2' 'renderproto' 'libpciaccess' 'libxv'
             'xf86dgaproto' 'libxmu' 'libxrender' 'libxi' 'dmxproto' 'libxaw' 'libdmx' 'libxtst' 'libxres'
             'xorg-xkbcomp' 'xorg-util-macros' 'xorg-font-util' 'glproto' 'dri2proto' 'libgcrypt' 'libepoxy'
             'xcb-util' 'xcb-util-image' 'xcb-util-renderutil' 'xcb-util-wm' 'xcb-util-keysyms' 'dri3proto'
             'libxshmfence' 'libunwind' 'libevdev')
source=($url/releases/individual/xserver/xorg-server-$pkgver.tar.bz2{,.sig}
        xvfb-run xvfb-run.1
        0001-Add-config-scan-mode-for-hotplug-support.patch)
validpgpkeys=('7B27A3F1A6E18CD9588B4AE8310180050905E40C'
              'C383B778255613DFDB409D91DB221A6900000011'
              'DD38563A8A8224537D1F90E45B8A2D50A0ECD0D3'
              '995ED5C8A6138EB0961F18474C09DD83CAAA50B2')
sha256sums=('9d967d185f05709274ee0c4f861a4672463986e550ca05725ce27974f550d3e6'
            'SKIP'
            '2198b9e635bb4e52bf986e678a702ec74f781b5b10c5713e1eeaa16cc9af078c'
            '2460adccd3362fefd4cdc5f1c70f332d7b578091fb9167bf88b5f91265bbd776'
            '8529fe33028f81d6ff89ca5f341cbc1807b936bc6d3f36c2d0610a7d4f701aed')

prepare() {
  cd "$pkgbase-$pkgver"

  patch -Np1 -i ../0001-Add-config-scan-mode-for-hotplug-support.patch
}

build() {
  cd "$pkgbase-$pkgver"

  # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
  # With them, module fail to load with undefined symbol.
  # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/,-z,now}

  autoreconf -fi
  ./configure --prefix=/usr \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-libunwind \
      --enable-xorg \
      --enable-xephyr \
      --enable-glamor \
      --disable-xwayland \
      --enable-kdrive \
      --enable-kdrive-kbd \
      --enable-kdrive-mouse \
      --disable-config-udev \
      --enable-config-scan \
      --disable-systemd-logind \
      --enable-suid-wrapper \
      --disable-install-setuid \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --libexecdir=/usr/lib/xorg-server \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts \
      --with-sha1=libgcrypt

#      --without-dtrace \
#      --disable-linux-acpi --disable-linux-apm \

  make

  # Disable subdirs for make install rule to make splitting easier
  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -e 's/^XWAYLAND_SUBDIRS =.*/XWAYLAND_SUBDIRS =/' \
      -i hw/Makefile
}

package_xorg-server-jjk() {
  pkgdesc="Xorg X server -- w/ scan mode instead of udev integration"
  depends=(libepoxy libxdmcp libxfont2 libpciaccess libdrm pixman libgcrypt
           libxau xorg-server-common libxshmfence libgl libevdev libunwind)
  # see xorg-server-*/hw/xfree86/common/xf86Module.h for ABI versions
  # we provide major numbers that drivers can depend on and
  # /usr/lib/pkgconfig/xorg-server.pc in xorg-server-devel pkg
  provides=('X-ABI-VIDEODRV_VERSION=24.0' 'X-ABI-XINPUT_VERSION=24.1'
            'X-ABI-EXTENSION_VERSION=10.0' 'x-server' xorg-server)
  groups=('xorg')
  conflicts=('nvidia-utils<=331.20' 'glamor-egl' xorg-server)
  replaces=('glamor-egl')
  install=xorg-server.install

  cd "$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install

  chown :210 "$pkgdir"/usr/lib/xorg-server/Xorg
  chmod g+s "$pkgdir"/usr/lib/xorg-server/Xorg

  # distro specific files must be installed in /usr/share/X11/xorg.conf.d
  install -m755 -d "$pkgdir/etc/X11/xorg.conf.d"
  install -m755 -d "$pkgdir/usr/share/X11/xorg.conf.d"

  # Needed for non-mesa drivers, libgl will restore it
  mv "$pkgdir/usr/lib/xorg/modules/extensions/libglx.so" \
     "$pkgdir/usr/lib/xorg/modules/extensions/libglx.xorg"

  rm -rf "$pkgdir/var"

  rm -f "$pkgdir/usr/share/man/man1/Xserver.1"
  rm -f "$pkgdir/usr/lib/xorg/protocol.txt"

  install -m755 -d "$pkgdir/usr/share/licenses/xorg-server"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server/COPYING"

  rm -rf "$pkgdir/usr/lib/pkgconfig"
  rm -rf "$pkgdir/usr/include"
  rm -rf "$pkgdir/usr/share/aclocal"
}

package_xorg-server-xephyr-jjk() {
  pkgdesc="A nested X server that runs as an X application"
  depends=(libxfont2 libgl libepoxy libgcrypt libxv pixman libunwind xorg-server-common
           'xcb-util-image' 'xcb-util-renderutil' 'xcb-util-wm' 'xcb-util-keysyms')

  cd "$pkgbase-$pkgver/hw/kdrive"
  make DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/licenses/xorg-server-xephyr"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xephyr/COPYING"
}
