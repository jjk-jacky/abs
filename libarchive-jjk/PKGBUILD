# $Id$
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Dan McGee <dan@archlinux.org>

_pkgname=libarchive
pkgname=$_pkgname-jjk
pkgver=3.3.3
pkgrel=1
pkgdesc='Multi-format archive and compression library'
arch=(x86_64)
url='https://libarchive.org/'
license=(BSD)
depends=(acl attr bzip2 expat lz4 openssl xz zlib)
provides=($_pkgname libarchive.so)
conflicts=($_pkgname)
source=("https://libarchive.org/downloads/libarchive-$pkgver.tar.gz")
sha512sums=('9d12b47d6976efa9f98e62c25d8b85fd745d4e9ca7b7e6d36bfe095dfe5c4db017d4e785d110f3758f5938dad6f1a1b009267fd7e82cb7212e93e1aea237bab7')

prepare() {
  cd libarchive-$pkgver
  autoreconf -fi
}

build() {
  cd libarchive-$pkgver

  ./configure \
      --prefix=/usr \
      --without-xml2 \
      --without-nettle \
      --disable-static

  make
}

check() {
  cd libarchive-$pkgver
  make check
}

package() {
  cd libarchive-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/libarchive/COPYING"
}
