# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub@gmail.com>

_pkgname=firefox
pkgname=$_pkgname-jjk
pkgver=52.9.0esr
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
url="https://www.mozilla.org/firefox/"
depends=('gtk2' 'gtk3' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types'
         'dbus-glib' 'alsa-lib' 'desktop-file-utils' 'hicolor-icon-theme'
         'libvpx' 'nss' 'sqlite')
makedepends=('unzip' 'zip' 'diffutils' 'python2' 'yasm' 'mesa' 'imake' 'cargo'
             'xorg-server-xvfb' 'libpulse' 'autoconf2.13'
             'inetutils' 'gconf')
optdepends=('networkmanager: Location detection via available WiFi networks')
provides=($_pkgname)
conflicts=($_pkgname)
options=('!emptydirs' '!makeflags' '!strip')
source=(https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.xz
        firefox-$pkgver.source.tar.xz.sha512::https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/SHA512SUMS
        firefox-$pkgver.source.tar.xz.sha512.asc::https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/SHA512SUMS.asc
        mozconfig firefox.desktop firefox-install-dir.patch vendor.js
        no-crmf.diff use_noexcept_in_mozalloc.patch
        0001-Bug-1435212-Add-support-for-FFmpeg-4.0.-r-bryce.patch.xz
        firefox-20.0.1-fixed-loading-icon.png)
sha512sums=('FILE'
            '7420023c8c7363914bc8f5ef48b1164d035f0d6161dff0de9551bc40becb0896e69d7bd20ce75e0397e6793301695cfcbfb6cf2bdc3ad9ce04cf9efd626c86d4'
            'SKIP'
            '82142488f36dff31496df484b547382c6913539d33bd60ca49450473b792407c3179c538f51e8a4618f0c3223a1b5887f0e709d9f579889b66bee6477aff780b'
            '6338bd399df04dcbfd2d4150c59be8716aebcb60edbfd298ba8671059c4f02ffcec3e9f750255c951fb197edf6130b0c25a4157e27fe0087dad89b3d40eb2627'
            '266989b0c4a37254a40836a6193284a186230b48716907e4d249d73616f58382b258c41baa8c1ffc98d405f77bfafcd3438f749edcf391c7bd22185399adf4bd'
            'd927e5e882115c780aa0d45034cb1652eaa191d95c15013639f9172ae734245caae070018465d73fdf86a01601d08c9e65f28468621422d799fe8451e6175cb7'
            '091a150750bfd4382ed06a3d79c7029d2a64de52af62fa0d25de56e1e823ddb7f14d8daaf71b0069b845845067a4d1b98da1a8b8250606c352ceca9ae3777193'
            'a1c59949b4d9226fba5ad7d06970248ab513af7b04515efaae099e087f5b81b6d5847b4cb23835b7b3506ef737c32d688942253ca865125de7c762d84ff014c9'
            '5898a167c6f86deb66891ee2c9467aa36a273045908fb8d9571021e0295d227ed66e6c052c1432a43dded9cab185d565717d44f6b1afb8054cf5337df8e0a57d'
            'd51119170cc8fb99c50610a8e5e94f38a31722c1c1a2260ca32d8e376732e30c8e1deac7d8c599348892e783fb4c75ce8c38bbd238282b0c9da21608d902ba28')
validpgpkeys=('2B90598A745E992F315E22C58AB132963A06537A'
              '14F26682D0916CDD81E37B6D61B7B526D98F0353')

# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Arch Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact foutrelis@archlinux.org for
# more information.
_google_api_key=AIzaSyDwr302FpOSkGRpLlUpPThNTDPbXcIn_FM

prepare() {
  cd $_pkgname-$pkgver

  cp ../mozconfig .mozconfig
  patch -Np1 -i ../firefox-install-dir.patch

  # https://bugzilla.mozilla.org/show_bug.cgi?id=1371991
  patch -Np1 -i ../no-crmf.diff
  # https://hg.mozilla.org/mozilla-central/rev/ae7e3082d862
  patch -Np1 -i ../use_noexcept_in_mozalloc.patch
  # # https://bugzilla.mozilla.org/show_bug.cgi?id=1435212
  patch -Np1 -i ../0001-Bug-1435212-Add-support-for-FFmpeg-4.0.-r-bryce.patch

  echo -n "$_google_api_key" >google-api-key
  echo "ac_add_options --with-google-api-keyfile=\"$PWD/google-api-key\"" >>.mozconfig

  mkdir "$srcdir/path"
  ln -s /usr/bin/python2 "$srcdir"/path/python

  # Use gold, as Mozilla can use some of its features, such as safe ICF
  #ln -s /usr/bin/ld.gold "$srcdir/path/ld"

  # Fix tab loading icon (flickers with libpng 1.6)
  # https://bugzilla.mozilla.org/show_bug.cgi?id=841734
  cp "$srcdir/firefox-20.0.1-fixed-loading-icon.png" \
    browser/themes/linux/tabbrowser/loading.png
}

build() {
  cd $_pkgname-$pkgver

  export PATH="$srcdir/path:$PATH"

  # _FORTIFY_SOURCE causes configure failures
  CPPFLAGS+=" -O2"
  # Hardening
  LDFLAGS+=" -Wl,-z,now"

  # Set up PGO
#  xvfb-run -a -s "-extension GLX -screen 0 1280x1024x24" \
    make -f client.mk build #MOZ_PGO=1
}

package() {
  cd $_pkgname-$pkgver
  make -f client.mk DESTDIR="$pkgdir" INSTALL_SDK= install

  install -Dm644 ../vendor.js "$pkgdir/usr/lib/firefox/browser/defaults/preferences/vendor.js"

  for i in 16 22 24 32 48 256; do
      install -Dm644 browser/branding/official/default$i.png \
        "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
  done
  install -Dm644 browser/branding/official/content/icon64.png \
    "$pkgdir/usr/share/icons/hicolor/64x64/apps/firefox.png"
  install -Dm644 browser/branding/official/mozicon128.png \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/firefox.png"
  install -Dm644 browser/branding/official/content/about-logo.png \
    "$pkgdir/usr/share/icons/hicolor/192x192/apps/firefox.png"
  install -Dm644 browser/branding/official/content/about-logo@2x.png \
    "$pkgdir/usr/share/icons/hicolor/384x384/apps/firefox.png"

  install -Dm644 ../firefox.desktop \
    "$pkgdir/usr/share/applications/firefox.desktop"

  #workaround for now
  #https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf firefox "$pkgdir/usr/lib/firefox/firefox-bin"
}
