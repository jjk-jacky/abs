# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: John Proctor <jproctor@prium.net>

_pkgname=libxml2
pkgname=$_pkgname-jjk
pkgver=2.9.8
pkgrel=2
pkgdesc="XML parsing library, version 2"
arch=('i686' 'x86_64')
license=('MIT')
depends=('zlib' 'readline' 'ncurses' 'xz')
options=('!makeflags')
makedepends=('python2' 'python')
provides=($_pkgname=$pkgver)
conflicts=($_pkgname)
url="http://www.xmlsoft.org/"
source=(ftp://xmlsoft.org/libxml2/libxml2-$pkgver.tar.gz{,.asc}
        https://www.w3.org/XML/Test/xmlts20130923.tar.gz)
validpgpkeys=('C74415BA7C9C7F78F02E1DC34606B8A5DE95BC1F')
sha256sums=('0b74e51595654f958148759cfef0993114ddccccbb6f31aee018f3558e8e2732'
            'SKIP'
            '9b61db9f5dbffa545f4b8d78422167083a8568c59bd1129f94138f936cf6fc1f')

prepare() {
  mkdir build-py{2,3}
  cd $_pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

_build() {
  cd build-py$1
  ../$_pkgname-$pkgver/configure --prefix=/usr --with-threads --with-history \
      --with-python=/usr/bin/python$1
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
  PYTHONHASHSEED=0 make
  find doc -type f -exec chmod 0644 {} \;
  cd ..
}

build() {
  _build 2
  _build 3
}

check() {
  cd build-py2
  ln -s ../xmlconf
  make check
}

package() {
  make -C build-py2 DESTDIR="$pkgdir" install
  make -C build-py3/python DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/licenses/$_pkgname" -m644 build-py2/COPYING
}
