# Maintainer: jjacky
_pkgname=anopa
pkgname=$_pkgname-musl-git
pkgver=0.3.0
pkgrel=1
pkgdesc="init system/service manager built around s6 supervision suite [musl; GIT]"
arch=('i686' 'x86_64')
url="http://jjacky.com/anopa"
license=('GPL3+')
depends=('execline-musl' 's6-musl')
makedepends=('git' 'musl' 'skalibs-musl')
options=('staticlibs')
source=("git+https://github.com/jjk-jacky/${_pkgname}.git#branch=next")
sha256sums=('SKIP')
provides=($_pkgname)
conflicts=($_pkgname)

pkgver() {
    cd "$_pkgname"
    git describe --abbrev=4 --dirty | tr - .
}

build() {
  cd "$_pkgname"

  tools/gen-deps.sh >package/deps.mak 2>/dev/null

  export CPPFLAGS='-nostdinc -isystem /usr/lib/musl/include -isystem /usr/include'
  export CC="musl-gcc"
  ./configure --enable-static-libc --bindir=/usr/bin --libexecdir=/usr/lib/anopa
  make
}

package() {
  cd "$_pkgname"

  make DESTDIR="$pkgdir/" install
  for name in reboot halt poweroff; do
      ln -s aa-shutdown "$pkgdir"/usr/bin/$name
  done
}

# vim:set ts=2 sw=2 et:
